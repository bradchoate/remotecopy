#!/usr/bin/perl

use strict;
use warnings;

use Digest::MD5 qw(md5_hex);
use Getopt::Long;
use IO::Socket;
use Pod::Usage;

Getopt::Long::Configure("no_auto_abbrev");

my %options;
my $opts_ok
    = GetOptions( \%options, 'help|?|h', 'man', 'port|p=s', 'address|a=s', );

pod2usage(2) if !$opts_ok;
pod2usage(1) if exists $options{help};
pod2usage( -exitstatus => 0, -verbose => 2 ) if exists $options{man};

my $config = load_config();

my $secret  = generate_secret();
my $address = $config->{server_address} || $options{address} || '127.0.0.1';
my $port    = $config->{port} || $options{port} || 12345;

my $growlnotify_location = `/usr/bin/which growlnotify`;
chomp $growlnotify_location;

my $sock = IO::Socket::INET->new(
    Proto     => 'tcp',
    LocalAddr => $address,
    LocalPort => $port,
    ReuseAddr => 1,
    Listen    => 1
) or die "couldn't create socket: $!";

while ( my $client = $sock->accept() ) {

    print $client "HELLO 0.1\n";

    my $buffer;
    my $copydata;
    $client->recv( $buffer, 512 );

    my ( $client_proto_version, $client_secret ) = split( /:/, $buffer );
    if ( $client_proto_version == 1 ) {
        if ( $client_secret eq $secret ) {
            print $client "SUCCESS\n";
        }
        else {
            print $client "FAILURE AUTH\n";
            copy($secret);
            notify(
                "Unauthenticated copy attempt, copied secret to clipboard");
            $client->close();
            next;
        }
    }
    else {
        print $client "FAILURE PROTOVER\n";
        notify(
            sprintf(
                "Unknown client version %d, rejecting.\nData: %s",
                $client_proto_version, $buffer
            )
        );
        $client->close();
        next;
    }

    $buffer = '';

    do {
        $copydata .= $buffer;
        $client->recv( $buffer, 512 );
    } while ($buffer);

    #print STDERR "all data: $copydata\n";

    copy($copydata);
    notify("Remote copy.");

    $client->close();
}

sub copy {
    my $copydata = shift;

    my $pbcopy;
    open( $pbcopy, "|pbcopy" ) or die "unable to open $!";
    print $pbcopy $copydata;
    close($pbcopy);
}

sub notify {
    my $message = shift;

    if ($growlnotify_location) {
        system( $growlnotify_location, "-m", $message );
    }
    else {
        print "$message\n";
    }
}

sub generate_secret {

    my $salt;

    open( my $urandom, '<', '/dev/urandom' );
    $salt .= getc $urandom for ( 0 .. 20 );
    close($urandom);

    return 'rc-' . md5_hex( $salt . time );
}

# same method in server and client
sub load_config {
    my $config_filepath = "$ENV{HOME}/.remotecopyrc";

    my $config = {};
    if ( -e $config_filepath ) {
        open( my $config_file, '<', $config_filepath );
        while ( my $line = <$config_file> ) {
            chomp($line);
            my ( $key, $option ) = $line =~ m/([\w]+)\s*=\s*(.*)/;
            $config->{$key} = $option;
        }
        close($config_file);
    }

    return $config;
}

__END__

=head1 NAME

remotecopyserver - local daemon listening for remote copy requests

=head1 SYNOPSIS

 remotecopyserver [options]

 Options:
  -p --port <port>          Port to listen on (defaults to 12345).
  -a --address <ip addr>    IP address to listen on (defaults to 127.0.0.1).

 Documentation options:
  -h --help -?              brief help message
     --man                  full documentation

=head1 REQUIRED ARGUMENTS

No arguments are required.

=head1 DESCRIPTION

This script implements a simple daemon which listens for remote copy requests.
It uses a minimal protocol handshake to make sure the remote end is allowed to
send data.

=head1 PROTOCOL

To be documented.

=head1 RC

If a ~/.remotecopyrc file is present, it will be read for options.  The file
format is just keys and values, separated by an equals sign. Example:

    server_address = 127.0.0.1
    port = 54321

The remotecopyserver will use two keys:

=over 4

=item server_address - IP address to listen on.

=item port - Port to listen on.

=back

=head1 AUTHOR

Nate Jones E<lt>nate@endot.orgE<gt>

=head1 COPYRIGHT

Copyright (c) 2011 by Nate Jones E<lt>nate@endot.orgE<gt>.

This program is free software; you can use, modify, and redistribute it under
the Artistic License, version 2.0.

See http://www.opensource.org/licenses/artistic-license-2.0.php

=cut
